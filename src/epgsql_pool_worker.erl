-module(epgsql_pool_worker).
-behaviour(gen_server).
-behaviour(poolboy_worker).

-export([start_link/1, squery/2, equery/3]).
-export([init/1, handle_call/3, handle_cast/2, handle_info/2, terminate/2,
         code_change/3]).

-record(state, {conn}).

start_link(Args) ->
    gen_server:start_link(?MODULE, Args, []).

squery(Worker, Sql) ->
    gen_server:call(Worker, {squery, Sql}, infinity).

equery(Worker, Stmt, Params) ->
    gen_server:call(Worker, {equery, Stmt, Params}, infinity).

init(Args) ->
    Hostname = proplists:get_value(hostname, Args),
    Database = proplists:get_value(database, Args),
    Username = proplists:get_value(username, Args),
    Password = proplists:get_value(password, Args),
    Timeout = proplists:get_value(timeout, Args),
    {ok, Conn} = pgsql:connect(Hostname, Username, Password, [
        {database, Database},
        {timeout, Timeout}
    ]),
    {ok, #state{conn=Conn}}.

handle_call({squery, Sql}, _From, #state{conn=Conn}=State) ->
    {reply, pgsql:squery(Conn, Sql), State};
handle_call({equery, Stmt, Params}, _From, #state{conn=Conn}=State) ->
    {reply, pgsql:equery(Conn, Stmt, Params), State};
handle_call(_Request, _From, State) ->
    {reply, ok, State}.

handle_cast(_Msg, State) ->
    {noreply, State}.

handle_info(_Info, State) ->
    {noreply, State}.

terminate(_Reason, #state{conn=Conn}) ->
    ok = pgsql:close(Conn),
    ok.

code_change(_OldVsn, State, _Extra) ->
    {ok, State}.
